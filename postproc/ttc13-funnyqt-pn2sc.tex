\documentclass[submission]{eptcs}
\providecommand{\event}{Transformation Tool Contest 2013 (TTC'13)}
\def\titlerunning{Solving the Petri-Nets to Statecharts Transformation Case with FunnyQT}
\def\authorrunning{Tassilo Horn}

\usepackage[utf8]{inputenc}
\usepackage[T1]{fontenc}
\usepackage{hyperref}
\usepackage{paralist}
\usepackage{verbatim}
\usepackage{footmisc}

\makeatletter
\def\verbatim@font{\ttfamily\small}
\makeatother

\usepackage{minted}
\newminted{clojure}{fontsize=\footnotesize,frame=lines,linenos}

\title{Solving the Petri-Nets to Statecharts Transformation Case with FunnyQT}
\author{Dipl.-Inform. Tassilo Horn
  \email{horn@uni-koblenz.de}
  \institute{Institute for Software Technology, University Koblenz-Landau, Germany}}

\clubpenalty = 10000
\widowpenalty = 10000
\displaywidowpenalty = 10000

%% Reduce the space between image and captions
\setlength\abovecaptionskip{0.1cm}
\setlength\belowcaptionskip{0cm}


\begin{document}

\maketitle

\begin{abstract}
  FunnyQT is a model querying and model transformation library for the
  functional Lisp-dialect Clojure providing a rich and efficient querying and
  transformation API.

  This paper describes the FunnyQT solution to the TTC 2013 Petri-Nets to
  Statcharts Transformation Case.  This solution has won the \emph{best overall
    solution award} and the \emph{best efficiency award} for this case.
\end{abstract}

\section{Introduction}
\label{sec:introduction}

\emph{FunnyQT} is a new model querying and transformation approach which is
implemented as an API for the functional, JVM-based Lisp-dialect Clojure.  It
provides several sub-APIs for implementing different kinds of queries and
transformations.  For example, there is a model-to-model transformation API,
and there is an in-place transformation API for writing programmed graph
transformations.  FunnyQT currently supports EMF and JGraLab models, and it can
be extended to other modeling frameworks, too.

For solving the tasks of this transformation case\footnote{This FunnyQT
  solution is available at \url{https://github.com/tsdh/ttc-2013-pn2sc} and on
  SHARE (image
  \textsf{TTC13::Ubuntu12LTS\_TTC13::FunnyQT.vdi})\label{fn:github}}, FunnyQT's
model transformation API has been used for the initialization transformation,
while the reduction transformation has been tackled algorithmically using the
plain querying and model manipulation APIs.  This solution has won the
\emph{best overall solution award} and the \emph{best efficiency award} for
this case.

\section{The Initialization Transformation}
\label{sec:init-transformation}

The initialization transformation using FunnyQT's model transformation API is
shown in Listing~\ref{lst:init}.  This API provides an internal DSL
\cite{book:Fowler2010DSL} for model-to-model transformations similar to ATL
\cite{ATL05} or ETL \cite{booklet:epsilon}.

A transformation is declared with the \verb|deftransformation| macro.  It
receives the name of the transformation, i.e., \verb|initialize-statechart|, a
vector of input and output models, and arbitrary many rules.  Here, the
argument vector declares that the transformation receives one single input
model \verb|pn| which is an EMF model, and it receives exactly one output model
\verb|sc| which is also an EMF model.  It could also receive many input and
output models, and those could belong to different modeling frameworks as well.

The transformation consists of two rules: \verb|place2basic-and-or|, and
\verb|transition2hyperedge|.  The former receives an input \verb|Place| and
creates an \verb|OR| and a \verb|Basic| in the output model.  It also sets the
new basic's name to the name of the place and assigns it as content of the new
\verb|OR|.  Finally, it sets the basic's \verb|rnext| and \verb|next|
references to the value of applying \verb|transition2hyperedge| to any
pre-transition or post-transition of the input place.
\verb|place2basic-and-or| is a top-level rule meaning it's applied
automatically to all matching elements while the non-top-level rule
\verb|transition2hyperedge| has to be called explicitly from other rules.

\begin{listing}[H]
  \begin{clojurecode}
(deftransformation initialize-statechart [[pn :emf] [sc :emf]]
  (^:top place2basic-and-or [p]
         :from 'Place
         :to [o 'OR, b 'Basic]
         (eset! b :name (eget p :name))
         (eset! b :rcontains o)
         (eset! b :rnext (map transition2hyperedge
                              (eget p :pret)))
         (eset! b :next  (map transition2hyperedge
                              (eget p :postt))))
  (transition2hyperedge [t]
         :from 'Transition
         :to [he 'HyperEdge]
         (eset! he :name (eget t :name))))
  \end{clojurecode}
  \label{lst:init}
  \caption{The initialization transformation}
\end{listing}

When a rule gets called and is applicable with respect to its declared
\verb|:from| type and \verb|:when| constraint, it creates the elements declared
in \verb|:to| in the target model, and evaluates its body.  In case there is
just one new element declared in \verb|:to|, it returns just that.  If there
are many new elements, it returns them as a vector in their declaration order.
Furthermore, a traceability mapping is created from the source element to the
rule's return value.  If a rule gets called multiple times for a single
element, the second and all following calls just return the result of the first
invocation.


\section{The Reduction Transformation}
\label{sec:reduction-transformation}

The reduction transformation is implemented algorithmically based on FunnyQT's
querying and model manipulation APIs.  It consists of four rules (functions):
\begin{compactenum}
\item The AND-rule as discussed in the case description \cite{pn2sccasedesc},
\item the OR-rule as discussed in the case description,
\item an additional, extension rule assigning hyperedges to the nearest
  \verb|Compound| state containing all their predecessor and successor
  \verb|Basic| states,
\item and a rule creating a \verb|Statechart| with an \verb|AND| top-state if
  the reduction could be completed successfully.
\end{compactenum}

In this section, only the AND- and OR-rule are discussed.  The main reduction
function simply applies them as long as possible, then invokes the hyperedge
assignment rule followed by the statechart creating rule.  The complete
reduction transformation is printed in Appendix~\ref{sec:reduction-code}.

\paragraph{Reduction Helper Functions.}
\label{sec:reduct-help-functions}

Before discussing the rules, some helper functions need to be introduced.
Those are \verb|pret| and \verb|postt| returning the sets of
pre-/post-transitions for a given place.  Likewise, \verb|prep| and
\verb|postp| return the sets of pre-/post-places for a given transition.

\paragraph{The AND Rule.}
\label{sec:and-rule}

The AND rule is depicted in Listing~\ref{lst:and-rule}.  In contrast to the
Figure~2 in the case description \cite{pn2sccasedesc}, it doesn't delete all
places $q_1$ to $q_n$ to create a new place $p$, but instead it reuses $q_1$ as
$p$ and deletes only $q_2$ to $q_n$, which is consistent with Louis Rose's EOL
solution.

The rule function receives the source Petri-net model \verb|pn|, the target
statechart model \verb|sc|, either the function \verb|prep| or \verb|postp| as
\verb|prep-or-postp|, and the traceability map \verb|place2or| gathered from
the initialization transformation mapping input places to output \verb|OR|
states wrapped in a Clojure atom\footnote{All Clojure data structures are
  immutable.  An atom is a mutable reference to some immutable data structure
  that can be swapped atomically.  This is important here in order to update
  the traceability mapping when the AND-rule matches.}.

\begin{listing}[ht]
  \begin{clojurecode}
(defn and-rule [pn sc prep-or-postp place2or]
  (loop [ts (eallobjects pn 'Transition), applied false]
    (if (seq ts)
      (let [t (first ts), preps-or-postps (prep-or-postp t)]
        (if (> (count preps-or-postps) 1)
          (let [p (first preps-or-postps), prets (pret p), postts (postt p)]
            (if (forall? #(and (= prets  (pret %))
                               (= postts (postt %)))
                         (rest preps-or-postps))
              (let [new-or  (ecreate! sc 'OR), new-and (ecreate! sc 'AND)]
                (eset! new-and :contains (mapv @place2or preps-or-postps))
                (eadd! new-or  :contains new-and)
                (swap! place2or assoc p new-or)
                (doseq [op (rest preps-or-postps)]
                  (edelete! op))
                (recur (rest ts) true))
              (recur (rest ts) applied)))
          (recur (rest ts) applied)))
      applied)))
  \end{clojurecode}
  \label{lst:and-rule}
  \caption{The AND rule}
\end{listing}

The rule iterates over all transitions in the petri-net model \verb|pn| using a
local tail-recursion (\verb|loop| and \verb|recur|).  Lines 5 to 9 check the
preconditions of the rule: If the transition \verb|t| has more than one
pre-/post-place, all of them must have the same set of pre- and
post-transitions.  If that's the case, a new \verb|AND| and a new \verb|OR|
state is created.  The new \verb|AND| contains all existing \verb|OR| states
being the pre- or post-places of the transition \verb|t|, and the new \verb|OR|
contains the new \verb|AND|.  Furthermore, the traceability map atom is updated
in line 13 so that the first pre-/post-place \verb|p| now maps to the new
\verb|OR|.  Lastly, all other pre-/post-places are deleted.


\paragraph{The OR Rule.}
\label{sec:or-rule}

The OR rule is depicted in Listing~\ref{lst:or-rule}.  In contrast to the case
description, it doesn't delete the places (or corresponding OR states) $q$ and
$r$ to create a new place (or corresponding OR state) $p$, but instead it
reuses $q$ as $p$ and only deletes $r$.

The \verb|or-rule| gets the Petri-net model \verb|pn|, the statechart model
\verb|sc|, and the traceability map atom \verb|place2or|.

\begin{listing}[h!]
  \begin{clojurecode}
(defn or-rule [pn sc place2or]
  (loop [ts (vec (eallobjects pn 'Transition)), applied false]
    (if (seq ts)
      (let [t (first ts), preps (prep t), postps (postp t)]
        (if (= 1 (count preps) (count postps))
          (let [q (first preps), r (first postps)]
            (if (or (identical? q r)
                    (and (not (member? r (adjs q :pret :postp)))
                         (not (member? r (adjs q :postt :prep)))))
              (let [merger (@place2or q), mergee (@place2or r)]
                (when-not (identical? q r)
                  (eaddall! q :pret  (eget-raw r :pret))
                  (eaddall! q :postt (eget-raw r :postt))
                  (edelete! r)
                  (eaddall! merger :contains (eget-raw mergee :contains))
                  (edelete! mergee))
                (edelete! t)
                (recur (rest ts) true))
              (recur (rest ts) applied)))
          (recur (rest ts) applied)))
      applied)))
  \end{clojurecode}
  \label{lst:or-rule}
  \caption{The OR rule}
\end{listing}

Like the AND-rule, it iterates all transitions in the petri-net model.  Lines 5
to 9 check the preconditions of the rule: If the transition \verb|t| has
exactly one pre-place \verb|q| and one post-place \verb|r|, and if \verb|q| and
\verb|r| are identical or \verb|q| and \verb|r| are not connected by other
transitions, then the rule matches.  In that case, the \verb|OR| corresponding
to \verb|r| is merged with the \verb|OR| corresponding to \verb|q|, and the
transition \verb|t| is deleted.


\paragraph{Extensions.}
\label{sec:extensions}

\begin{sloppypar}
  Two extensions were implemented for this task.  Firstly, there is an
  additional rule \verb|assign-hyperedges| that assigns each hyperedge to the
  nearest compound state which contains all basic states connected by the
  hyperedge.  Secondly, a validation
  tool\footnote{\url{https://github.com/tsdh/ttc-2013-pn2sc-validation}} has
  been implemented that uses FunnyQT to check result statechart models against
  their expected outcome.
\end{sloppypar}
\section{Evaluation}
\label{sec:evaluation}

In this section, the solution is evaluated according to the evaluation criteria
listed in the case description \cite{pn2sccasedesc}.

\paragraph{Transformation correctness.}

The validation project that has been implemented as an extension to this case
allows for testing the result statechart models.  For the main test cases,
every important aspect of the result models including the containment hierarchy
and the predecessors and successors of hyperedges are checked, and for the
performance test cases, only the number of instances of every metamodel class
is checked.  All tests pass for the result models of this solution.  Similarly,
all tests pass for the result models created by the reference GrGen.NET
solution.

The validation project has also been tested with intentionally slightly wrong
models, e.g., some \verb|next| link is missing at some hyperedge, there's some
additional element, or an element is contained by the wrong \verb|Compound|
state.  In all those cases, an assertion of the validation project failed.  So
there's a high confidence that if the result models pass the tests, the
transformation producing them is correct.

\paragraph{Transformation performance.}

This FunnyQT solution is by far the most efficient of all submitted solutions,
especially for large models, so it has won the \emph{best efficiency award} for
this case.  For the performance test models \verb|sp5000| and \verb|sp10000|
the complete transformation (initialization and reduction) takes about one and
two seconds, which is about as fast as the second fastest solution.  But with
the \verb|sp40000|/\verb|sp200000| model, the FunnyQT solution is already
three/twelve times faster than the second fastest solution, taking 11 and 114
seconds, respectively.

\paragraph{Transformation understandability.}

Although the solution requires some understanding of Clojure, it shouldn't be
hard to get a grasp on it.  The initialization transformation uses a FunnyQT
facility allowing to specify typical model transformations with a syntax and
semantics similar to ATL or ETL, so people knowing these languages should feel
right at home.

The reduction transformation is a bit more complex, but the application
conditions of the rules and the actions that are performed are taken quite
literally from the case description with the exception that some elements are
preserved and merged instead of replaced.

One important aspect with respect to understandability is also the fact that
the transformations are very concise.  In total, the initialization and the
reduction transformation are only 96 lines of code.

\paragraph{Bonus criteria.}

The bonus tasks dealing with \emph{verification}, \emph{simulation support},
\emph{change propagation}, and \emph{reversing the transformation} haven't been
tackled.

Proper \emph{debugging support} is also not yet ready for prime-time in the
Clojure world.  There are some attempts at debuggers allowing to set
breakpoints and examine the lexical extent around the breakpoint, but those are
not too usable right now.  Another difficulty with functional languages
involving some kind of laziness is that errors might be signaled at a location
very different to where the bug is actually manifested in the source code.
Nevertheless, FunnyQT has rather good model visualization tools that have been
used while programming the reduction rules in order to visualize the matching
elements when a rule has been applicable.


\bibliographystyle{alpha}
\bibliography{ttc13-funnyqt-pn2sc}

\appendix
\newpage

\section{The complete Initialization Transformation}
\label{sec:init-code}

\begin{clojurecode}
(deftransformation initialize-statechart [[pn :emf] [sc :emf]]
  (^:top place2basic-and-or [p]
         :from 'Place
         :to [o 'OR, b 'Basic]
         (eset! b :name (eget p :name))
         (eset! b :rcontains o)
         (eset! b :rnext (map transition2hyperedge (eget p :pret)))
         (eset! b :next  (map transition2hyperedge (eget p :postt))))
  (transition2hyperedge [t]
         :from 'Transition
         :to [he 'HyperEdge]
         (eset! he :name (eget t :name))))

(defn init-statechart [pn]
  (let [sc (new-model)
        trace (initialize-statechart pn sc)]
    [sc
     (apply hash-map (mapcat (fn [[p [o b]]] [p o])
                             (:place2basic-and-or trace)))
     (apply hash-map (mapcat (fn [[p [o b]]] [p b])
                             (:place2basic-and-or trace)))
     (:transition2hyperedge trace)]))
\end{clojurecode}

\section{The complete Reduction Transformation}
\label{sec:reduction-code}

\begin{clojurecode}
(defn refs-as-set [ref elem]
  (set (eget-raw elem ref)))

(def postt (partial refs-as-set :postt))
(def pret  (partial refs-as-set :pret))
(def postp (partial refs-as-set :postp))
(def prep  (partial refs-as-set :prep))

(defn and-rule [pn sc prep-or-postp place2or]
  (loop [ts (eallobjects pn 'Transition), applied false]
    (if (seq ts)
      (let [t (first ts), preps-or-postps (prep-or-postp t)]
        (if (> (count preps-or-postps) 1)
          (let [p (first preps-or-postps), prets (pret p), postts (postt p)]
            (if (forall? #(and (= prets  (pret %))
                               (= postts (postt %)))
                         (rest preps-or-postps))
              (let [new-or  (ecreate! sc 'OR), new-and (ecreate! sc 'AND)]
                (eset! new-and :contains (mapv @place2or preps-or-postps))
                (eadd! new-or  :contains new-and)
                (swap! place2or assoc p new-or)
                (doseq [op (rest preps-or-postps)]
                  (edelete! op))
                (recur (rest ts) true))
              (recur (rest ts) applied)))
          (recur (rest ts) applied)))
      applied)))

(defn or-rule [pn sc place2or]
  (loop [ts (vec (eallobjects pn 'Transition)), applied false]
    (if (seq ts)
      (let [t (first ts), preps (prep t), postps (postp t)]
        (if (= 1 (count preps) (count postps))
          (let [q (first preps), r (first postps)]
            (if (or (identical? q r)
                    (and (not (member? r (adjs q :pret :postp)))
                         (not (member? r (adjs q :postt :prep)))))
              (let [merger (@place2or q), mergee (@place2or r)]
                (when-not (identical? q r)
                  (eaddall! q :pret  (eget-raw r :pret))
                  (eaddall! q :postt (eget-raw r :postt))
                  (edelete! r)
                  (eaddall! merger :contains (eget-raw mergee :contains))
                  (edelete! mergee))
                (edelete! t)
                (recur (rest ts) true))
              (recur (rest ts) applied)))
          (recur (rest ts) applied)))
      applied)))

(defn assign-hyperedges [sc]
  (doseq [e (eallobjects sc 'HyperEdge)]
    (eset! e :rcontains
           (first (apply clojure.set/intersection
                         (map #(reachables % [p-+ --<>])
                              (concat (eget e :next) (eget e :rnext))))))))

(defn create-top [sc]
  (let [top-ors (filter #(not (eget % :rcontains)) (eallobjects sc 'OR))]
    (when (= 1 (count top-ors))
      (let [statechart (ecreate! sc 'Statechart), top (ecreate! sc 'AND)]
        (eset! statechart :topState top)
        (eset! top :contains top-ors)))))

(defn create-statechart [pn]
  (let [[sc place2or _ _] (init/init-statechart pn)
        place2or (atom place2or)]
    (iteratively (fn []
                   (let [r     (and-rule pn sc prep  place2or)
                         r (or (and-rule pn sc postp place2or) r)
                         r (or (or-rule  pn sc place2or)       r)]
                     r)))
    (create-top sc)
    (assign-hyperedges sc)
    sc))
\end{clojurecode}



\end{document}




%%% Local Variables:
%%% mode: latex
%%% TeX-engine: pdflatex-shell-escape
%%% TeX-master: t
%%% End:
